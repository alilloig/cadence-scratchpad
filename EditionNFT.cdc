import NonFungibleToken from "./lib/NonFungibleToken.cdc"
import MetadataViews from "./lib/MetadataViews.cdc"
import FungibleToken from "./lib/FungibleToken.cdc"

pub contract Foo: NonFungibleToken {

    // Events
    //
    pub event ContractInitialized()
    pub event Withdraw(id: UInt64, from: Address?)
    pub event Deposit(id: UInt64, to: Address?)
    pub event Minted(id: UInt64)
    pub event Revealed(id: UInt64)
    pub event EditionCreated(id: UInt64)

    // Named Paths
    //
    pub let CollectionStoragePath: StoragePath
    pub let CollectionPublicPath: PublicPath
    pub let CollectionPrivatePath: PrivatePath
    pub let AdminStoragePath: StoragePath

    // totalSupply
    // The total number of Foo NFTs that have been minted.
    //
    pub var totalSupply: UInt64

    // totalEditions
    // The total number of Foo editions that have been minted.
    //
    pub var totalEditions: UInt64

    // A placeholder image used to display NFTs that have not
    // yet been revealed.
    pub let placeholderImage: String

    pub struct Edition {
        pub let id: UInt64
        pub let size: UInt

        pub let name: String
        pub let description: String
        pub let thumbnail: String

        init(
            id: UInt64,
            size: UInt,
            name: String,
            description: String,
            thumbnail: String,
        ) {
            self.id = id
            self.size = size

            self.name = name
            self.description = description
            self.thumbnail = thumbnail
        }
    }

    access(self) let editions: {UInt64: Edition}

    pub fun getEdition(id: UInt64): Edition? {
        return Foo.editions[id]
    }

    pub struct EditionMember {
        pub let editionID: UInt64
        pub let editionSerial: UInt64
        pub let editionSalt: String

        init(
            editionID: UInt64,
            editionSerial: UInt64,
            editionSalt: String,
        ) {
            self.editionID = editionID
            self.editionSerial = editionSerial
            self.editionSalt = editionSalt
        }

        pub fun getData(): Edition {
            return Foo.getEdition(id: self.editionID)!
        }
    }

    access(self) let editionMembers: {UInt64: EditionMember}

    pub fun getEditionMember(id: UInt64): EditionMember? {
        return Foo.editionMembers[id]
    }

    pub resource NFT: NonFungibleToken.INFT {

        pub let id: UInt64

        // A hash of the NFT's edition membership.
        //
        // The edition hash is known at mint time and 
        // is generated by hashing the edition ID and
        // serial number for this NFT.
        pub let editionHash: String

        init(
            id: UInt64,
            editionHash: String
        ) {
            self.id = id
            self.editionHash = editionHash
        }

        // Return the edition that this NFT belongs to.
        //
        // This function returns nil if the edition membership has
        // not yet been revealed.
        pub fun getEdition(): EditionMember? {
            return Foo.editionMembers[self.id]
        }

        pub fun getViews(): [Type] {
            return [
                Type<MetadataViews.Display>()
            ]
        }

        pub fun resolveView(_ view: Type): AnyStruct? {
            switch view {
                case Type<MetadataViews.Display>():
                    return self.resolveDisplay()
            }

            return nil
        }

        pub fun resolveDisplay(): MetadataViews.Display {
            if let edition = self.getEdition() {
                let data = edition.getData()

                return MetadataViews.Display(
                    name: data.name,
                    description: data.description,
                    thumbnail: MetadataViews.IPFSFile(
                        cid: data.thumbnail, 
                        nil
                    )
                )
            }

            return MetadataViews.Display(
                name: "Foo",
                description: "This NFT is not yet revealed.",
                thumbnail: MetadataViews.IPFSFile(
                    cid: Foo.placeholderImage, 
                    nil
                )
            )
        }
    }

    pub resource interface FooCollectionPublic {
        pub fun deposit(token: @NonFungibleToken.NFT)
        pub fun getIDs(): [UInt64]
        pub fun borrowNFT(id: UInt64): &NonFungibleToken.NFT
        pub fun borrowFoo(id: UInt64): &Foo.NFT? {
            post {
                (result == nil) || (result?.id == id):
                    "Cannot borrow Foo reference: The ID of the returned reference is incorrect"
            }
        }
    }

    pub resource Collection: FooCollectionPublic, NonFungibleToken.Provider, NonFungibleToken.Receiver, NonFungibleToken.CollectionPublic {
        
        // dictionary of NFTs
        // NFT is a resource type with an `UInt64` ID field
        //
        pub var ownedNFTs: @{UInt64: NonFungibleToken.NFT}

        // withdraw
        // Removes an NFT from the collection and moves it to the caller
        //
        pub fun withdraw(withdrawID: UInt64): @NonFungibleToken.NFT {
            let token <- self.ownedNFTs.remove(key: withdrawID) ?? panic("missing NFT")

            emit Withdraw(id: token.id, from: self.owner?.address)

            return <- token
        }

        // deposit
        // Takes a NFT and adds it to the collections dictionary
        // and adds the ID to the id array
        //
        pub fun deposit(token: @NonFungibleToken.NFT) {
            let token <- token as! @Foo.NFT

            let id: UInt64 = token.id

            // add the new token to the dictionary which removes the old one
            let oldToken <- self.ownedNFTs[id] <- token

            emit Deposit(id: id, to: self.owner?.address)

            destroy oldToken
        }

        // getIDs
        // Returns an array of the IDs that are in the collection
        //
        pub fun getIDs(): [UInt64] {
            return self.ownedNFTs.keys
        }

        // borrowNFT
        // Gets a reference to an NFT in the collection
        // so that the caller can read its metadata and call its methods
        //
        pub fun borrowNFT(id: UInt64): &NonFungibleToken.NFT {
            return (&self.ownedNFTs[id] as &NonFungibleToken.NFT?)!
        }

        // borrowFoo
        // Gets a reference to an NFT in the collection as a Foo.
        //
        pub fun borrowFoo(id: UInt64): &Foo.NFT? {
            if self.ownedNFTs[id] != nil {
                let ref = (&self.ownedNFTs[id] as auth &NonFungibleToken.NFT?)!
                return ref as! &Foo.NFT
            }

            return nil
        }

        // destructor
        destroy() {
            destroy self.ownedNFTs
        }

        // initializer
        //
        init () {
            self.ownedNFTs <- {}
        }
    }

    // createEmptyCollection
    // public function that anyone can call to create a new empty collection
    //
    pub fun createEmptyCollection(): @NonFungibleToken.Collection {
        return <- create Collection()
    }

    // Admin
    // Resource that an admin can use to mint NFTs.
    //
    pub resource Admin {

        // createEdition
        //
        // Create a new NFT edition.
        //
        // This function does not mint any NFTs. It only creates the
        // edition data that will later be associated with minted NFTs.
        //
        pub fun createEdition(
            size: UInt,
            name: String,
            description: String,
            thumbnail: String,
        ): UInt64 {
            let edition = Edition(
                id: Foo.totalEditions,
                size: size,
                name: name,
                description: description,
                thumbnail: thumbnail,
            )

            Foo.editions[edition.id] = edition

            emit EditionCreated(id: edition.id)

            Foo.totalEditions = Foo.totalEditions + (1 as UInt64)

            return edition.id
        }

        // mintNFT
        //
        // Mints a new NFT.
        //
        pub fun mintNFT(editionHash: String): @Foo.NFT {
            let nft <- create Foo.NFT(
                id: Foo.totalSupply,
                editionHash: editionHash,
            )

            emit Minted(id: nft.id)

            Foo.totalSupply = Foo.totalSupply + (1 as UInt64)

            return <- nft
        }

        pub fun revealNFT(
            id: UInt64,
            editionID: UInt64,
            editionSerial: UInt64,
            editionSalt: String
        ) {
            pre {
                Foo.editionMembers[id] == nil : "NFT has already been revealed"
            }

            let editionMember = EditionMember(
                editionID: editionID,
                editionSerial: editionSerial,
                editionSalt: editionSalt,
            )

            Foo.editionMembers[id] = editionMember

            emit Revealed(id: id)
        }
    }

    // fetch
    // Get a reference to a Foo from an account's Collection, if available.
    // If an account does not have a Foo.Collection, panic.
    // If it has a collection but does not contain the itemID, return nil.
    // If it has a collection and that collection contains the itemID, return a reference to that.
    //
    pub fun fetch(_ from: Address, itemID: UInt64): &Foo.NFT? {
        let collection = getAccount(from)
            .getCapability(Foo.CollectionPublicPath)!
            .borrow<&{ Foo.FooCollectionPublic }>()
            ?? panic("Couldn't get collection")

        // We trust Foo.Collection.borowFoo to get the correct itemID
        // (it checks it before returning it).
        return collection.borrowFoo(id: itemID)
    }

    // initializer
    //
    init(admin: AuthAccount, placeholderImage: String) {
        // Set our named paths
        self.CollectionStoragePath = /storage/FooCollection
        self.CollectionPublicPath = /public/FooCollection
        self.CollectionPrivatePath = /private/FooCollection
        self.AdminStoragePath = /storage/FooAdmin

        self.placeholderImage = placeholderImage

        // Initialize the total supply
        self.totalSupply = 0

        // Initialize the total editions
        self.totalEditions = 0

        self.editions = {}
        self.editionMembers = {}
        
        let collection <- Foo.createEmptyCollection()
        
        admin.save(<- collection, to: Foo.CollectionStoragePath)

        admin.link<&Foo.Collection>(Foo.CollectionPrivatePath, target: Foo.CollectionStoragePath)

        admin.link<&Foo.Collection{NonFungibleToken.CollectionPublic, Foo.FooCollectionPublic}>(Foo.CollectionPublicPath, target: Foo.CollectionStoragePath)
        
        // Create an admin resource and save it to storage
        let adminResource <- create Admin()
        admin.save(<- adminResource, to: self.AdminStoragePath)

        emit ContractInitialized()
    }
}
