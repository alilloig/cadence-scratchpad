import NonFungibleToken from "../../../lib/NonFungibleToken.cdc"
import MetadataViews from "../../../lib/MetadataViews.cdc"
import FungibleToken from "../../../lib/FungibleToken.cdc"

pub contract Foo: NonFungibleToken {

    // Events
    //
    pub event ContractInitialized()
    pub event Withdraw(id: UInt64, from: Address?)
    pub event Deposit(id: UInt64, to: Address?)
    pub event Minted(id: UInt64)
    pub event Revealed(id: UInt64)

    // Named Paths
    //
    pub let CollectionStoragePath: StoragePath
    pub let CollectionPublicPath: PublicPath
    pub let CollectionPrivatePath: PrivatePath
    pub let AdminStoragePath: StoragePath

    // totalSupply
    // The total number of Foo that have been minted
    //
    pub var totalSupply: UInt64

    // A placeholder image used to display NFTs that have not
    // yet been revealed.
    pub let placeholderImage: String

    pub struct Metadata {

        // A salt that is published when the metadata is revealed.
        //
        // The salt is a hex-encoded byte array that is prepended to the 
        // encoded metadata values before generating the metadata hash.
        pub let metadataSalt: String

        pub let name: String
        pub let description: String
        pub let thumbnail: String

        init(
            metadataSalt: String,
            name: String,
            description: String,
            thumbnail: String,
        ) {
            self.metadataSalt = metadataSalt

            self.name = name
            self.description = description
            self.thumbnail = thumbnail
        }
    }

    access(contract) let metadata: {UInt64: Metadata}

    pub resource NFT: NonFungibleToken.INFT {

        pub let id: UInt64

        // A hash of the NFT's metadata.
        //
        // The metadata hash is known at mint time and 
        // is generated by hashing the set of metadata fields
        // for this NFT. The hash can later be used to verify
        // that the correct metadata fields are revealed.
        pub let metadataHash: String

        init(
            id: UInt64,
            metadataHash: String
        ) {
            self.id = id
            self.metadataHash = metadataHash
        }

        // Return the metadata for this NFT.
        //
        // This function returns nil if the NFT metadata has
        // not yet been revealed.
        pub fun getMetadata(): Metadata? {
            return Foo.metadata[self.id]
        }

        pub fun getViews(): [Type] {
            return [
                Type<MetadataViews.Display>()
            ]
        }

        pub fun resolveView(_ view: Type): AnyStruct? {
            switch view {
                case Type<MetadataViews.Display>():
                    return self.resolveDisplay()
            }

            return nil
        }

        pub fun resolveDisplay(): MetadataViews.Display {
            if let metadata = self.getMetadata() {
                return MetadataViews.Display(
                    name: metadata.name,
                    description: metadata.description,
                    thumbnail: MetadataViews.IPFSFile(
                        cid: metadata.thumbnail, 
                        nil
                    )
                )
            }

            return MetadataViews.Display(
                name: "Foo",
                description: "This NFT is not yet revealed.",
                thumbnail: MetadataViews.IPFSFile(
                    cid: Foo.placeholderImage, 
                    nil
                )
            )
        }
    }

    pub resource interface FooCollectionPublic {
        pub fun deposit(token: @NonFungibleToken.NFT)
        pub fun getIDs(): [UInt64]
        pub fun borrowNFT(id: UInt64): &NonFungibleToken.NFT
        pub fun borrowFoo(id: UInt64): &Foo.NFT? {
            post {
                (result == nil) || (result?.id == id):
                    "Cannot borrow Foo reference: The ID of the returned reference is incorrect"
            }
        }
    }

    pub resource Collection: FooCollectionPublic, NonFungibleToken.Provider, NonFungibleToken.Receiver, NonFungibleToken.CollectionPublic {
        
        // dictionary of NFTs
        // NFT is a resource type with an `UInt64` ID field
        //
        pub var ownedNFTs: @{UInt64: NonFungibleToken.NFT}

        // withdraw
        // Removes an NFT from the collection and moves it to the caller
        //
        pub fun withdraw(withdrawID: UInt64): @NonFungibleToken.NFT {
            let token <- self.ownedNFTs.remove(key: withdrawID) ?? panic("missing NFT")

            emit Withdraw(id: token.id, from: self.owner?.address)

            return <- token
        }

        // deposit
        // Takes a NFT and adds it to the collections dictionary
        // and adds the ID to the id array
        //
        pub fun deposit(token: @NonFungibleToken.NFT) {
            let token <- token as! @Foo.NFT

            let id: UInt64 = token.id

            // add the new token to the dictionary which removes the old one
            let oldToken <- self.ownedNFTs[id] <- token

            emit Deposit(id: id, to: self.owner?.address)

            destroy oldToken
        }

        // getIDs
        // Returns an array of the IDs that are in the collection
        //
        pub fun getIDs(): [UInt64] {
            return self.ownedNFTs.keys
        }

        // borrowNFT
        // Gets a reference to an NFT in the collection
        // so that the caller can read its metadata and call its methods
        //
        pub fun borrowNFT(id: UInt64): &NonFungibleToken.NFT {
            return (&self.ownedNFTs[id] as &NonFungibleToken.NFT?)!
        }

        // borrowFoo
        // Gets a reference to an NFT in the collection as a Foo.
        //
        pub fun borrowFoo(id: UInt64): &Foo.NFT? {
            if self.ownedNFTs[id] != nil {
                let ref = (&self.ownedNFTs[id] as auth &NonFungibleToken.NFT?)!
                return ref as! &Foo.NFT
            }

            return nil
        }

        // destructor
        destroy() {
            destroy self.ownedNFTs
        }

        // initializer
        //
        init () {
            self.ownedNFTs <- {}
        }
    }

    // createEmptyCollection
    // public function that anyone can call to create a new empty collection
    //
    pub fun createEmptyCollection(): @NonFungibleToken.Collection {
        return <- create Collection()
    }

    // Admin
    // Resource that an admin can use to mint NFTs.
    //
    pub resource Admin {

        // mintNFT
        // Mints a new NFT with a new ID
        //
        pub fun mintNFT(metadataHash: String): @Foo.NFT {
            let nft <- create Foo.NFT(
                id: Foo.totalSupply,
                metadataHash: metadataHash,
            )

            emit Minted(id: nft.id)

            Foo.totalSupply = Foo.totalSupply + (1 as UInt64)

            return <- nft
        }

        pub fun revealNFT(id: UInt64, metadata: Metadata) {
            pre {
                Foo.metadata[id] == nil : "NFT has already been revealed"
            }

            Foo.metadata[id] = metadata

            emit Revealed(id: id)
        }
    }

    // fetch
    // Get a reference to a Foo from an account's Collection, if available.
    // If an account does not have a Foo.Collection, panic.
    // If it has a collection but does not contain the itemID, return nil.
    // If it has a collection and that collection contains the itemID, return a reference to that.
    //
    pub fun fetch(_ from: Address, itemID: UInt64): &Foo.NFT? {
        let collection = getAccount(from)
            .getCapability(Foo.CollectionPublicPath)!
            .borrow<&{ Foo.FooCollectionPublic }>()
            ?? panic("Couldn't get collection")

        // We trust Foo.Collection.borowFoo to get the correct itemID
        // (it checks it before returning it).
        return collection.borrowFoo(id: itemID)
    }

    // initializer
    //
    init(admin: AuthAccount, placeholderImage: String) {
        // Set our named paths
        self.CollectionStoragePath = /storage/FooCollection
        self.CollectionPublicPath = /public/FooCollection
        self.CollectionPrivatePath = /private/FooCollection
        self.AdminStoragePath = /storage/FooAdmin

        self.placeholderImage = placeholderImage

        // Initialize the total supply
        self.totalSupply = 0

        self.metadata = {}

        let collection <- Foo.createEmptyCollection()
        
        admin.save(<- collection, to: Foo.CollectionStoragePath)

        admin.link<&Foo.Collection>(Foo.CollectionPrivatePath, target: Foo.CollectionStoragePath)

        admin.link<&Foo.Collection{NonFungibleToken.CollectionPublic, Foo.FooCollectionPublic}>(Foo.CollectionPublicPath, target: Foo.CollectionStoragePath)
        
        // Create an admin resource and save it to storage
        let adminResource <- create Admin()
        admin.save(<- adminResource, to: self.AdminStoragePath)

        emit ContractInitialized()
    }
}
